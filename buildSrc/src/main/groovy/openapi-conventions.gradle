import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id 'org.openapi.generator'
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-annotations'
  implementation 'jakarta.annotation:jakarta.annotation-api'
  implementation 'jakarta.servlet:jakarta.servlet-api'
  implementation 'jakarta.validation:jakarta.validation-api'
  implementation 'org.projectlombok:lombok'
  implementation 'org.openapitools:jackson-databind-nullable'
  implementation 'com.github.spotbugs:spotbugs-annotations'
  implementation 'org.springframework:spring-context'
  implementation 'org.springframework:spring-core'
  implementation 'org.springframework:spring-web'
  implementation 'org.springframework:spring-webflux'
  implementation 'io.swagger.core.v3:swagger-annotations'
  implementation 'io.swagger.core.v3:swagger-core'

  annotationProcessor 'io.swagger.core.v3:swagger-annotations'
  annotationProcessor 'org.projectlombok:lombok'
}

tasks.register('openApiGenerateSpring', GenerateTask) {
  generatorName = 'spring'
  inputSpec = "${projectDir}/src/main/spec/openapi.yaml"
  outputDir = "${buildDir}/generated/spring"
  configOptions.putAll([dateLibrary                   : 'java8',
                        interfaceOnly                 : 'true',
                        useSpringBoot3                : 'true',
                        containerDefaultToNull        : 'true',
                        additionalModelTypeAnnotations: '@lombok.experimental.SuperBuilder'])
}

tasks.register('openApiGenerateJavaClient', GenerateTask) {
  generatorName = 'java'
  inputSpec = "${projectDir}/src/main/spec/openapi.yaml"
  outputDir = "${buildDir}/generated/java-client"
  configOptions.putAll([dateLibrary                   : 'java8',
                        useJakartaEe                  : 'true',
                        library                       : 'webclient',
                        serializationLibrary          : 'jackson',
                        containerDefaultToNull        : 'true',
                        additionalModelTypeAnnotations: '@lombok.experimental.SuperBuilder'])
}

sourceSets {
  main {
    java {
      srcDirs += ["${buildDir}/generated/spring/src/main/java", "${buildDir}/generated/java-client/src/main/java"]
    }
    resources {
      srcDirs += "${projectDir}/src/main/spec"
    }
  }
}

compileJava.dependsOn << [tasks.named('openApiGenerateSpring'), tasks.named('openApiGenerateJavaClient')]

